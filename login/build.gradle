if (isRunAlone.toBoolean()) {
    apply plugin: 'com.starter.application.android'
    apply plugin: 'WMRouter'
} else {
    apply plugin: 'com.starter.library.android'
}

apply plugin: 'kotlin-kapt'

android {

    defaultConfig {
        if (isRunAlone.toBoolean()) {
            applicationId "org.nyjsl.zeus.login"
        }
        versionCode 1

    }

    sourceSets {
        main {
            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    resourcePrefix "login_"

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }


    flavorDimensions "default"
    productFlavors {
        // 开发
        dev {
            buildConfigField 'String', 'HOST_USER', '"http://devapi.xiaoliancloud.com/m/"'
        }
        // 测试
        qa {
            buildConfigField 'String', 'HOST_USER', '"http://testapi.xiaoliancloud.com/m/"'
        }
        // 预发
        pre{
            buildConfigField 'String', 'HOST_USER', '"https://preapi.xiaolianhb.com/m/"'
        }
        //正式
        prod {
            buildConfigField 'String', 'HOST_USER', '"https://api.xiaolianhb.com/m/"'
        }
        //正式 hw api
        hw {
            buildConfigField 'String', 'HOST_USER', '"https://hwapi.xiaoliancloud.com/m/"'
        }
    }
}

apply from: '../config.gradle'

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':common')
    implementation project(path: ':home')
    kapt deps.roomCompiler
    kapt deps.wmRouterCompiler


}